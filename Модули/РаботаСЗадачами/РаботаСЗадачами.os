#Использовать "../../lib/json"

Перем ИмяСтадииДо;
Процедура ПеренестиЗадачиВФинальнуюСтадию(НастройкиИзJson) Экспорт
	
	СтруктураПараметровТрекер = НастройкиИзJson.youtrack;
	Если Не СтруктураПараметровТрекер.use Тогда

		Возврат;
		
	КонецЕсли; 
	ИспользуемыеПроекты = СтруктураПараметровТрекер.projectNames;
	ИмяСтадииДо = СтруктураПараметровТрекер.stageBefore;


	ШаблонЗапросаЗадачи = "api/issues?fields=$type,id,summary,customFields($type,id,projectCustomField($type,id,field($type,id,name)),value($type,avatarUrl,buildLink,color(id),fullName,id,isResolved,localizedName,login,minutes,name,presentation,text))&query=project:+%7B#ИмяПроекта#%7D'";

	Для каждого ИмяПроекта Из ИспользуемыеПроекты Цикл
		ТекстЗапросаЗадачи = СтрЗаменить(ШаблонЗапросаЗадачи, "#ИмяПроекта#", ИмяПроекта.shortName);
		МассивЗадач = Неопределено;
		МассивЗадач = ПолучитьМассивЗадач(СтруктураПараметровТрекер, ТекстЗапросаЗадачи);
		Если МассивЗадач = Неопределено ИЛИ ТипЗнч(МассивЗадач) <> Тип("Массив") Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого СтруктураЗадачи Из МассивЗадач Цикл
			ЗадачаВНужнойСтадии = ЗадачаВНужнойСтадии(СтруктураЗадачи);
			Если ЗадачаВНужнойСтадии Тогда
				ПеренестиЗадачу(СтруктураПараметровТрекер, СтруктураЗадачи);
			КонецЕсли;
			
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Функция ЗадачаВНужнойСтадии(СтруктураЗадачи)
	ЗадачаВНужнойСтадии = Ложь;
	Если Не СтруктураЗадачи.Свойство("customFields") Тогда
		Возврат ЗадачаВНужнойСтадии;
	КонецЕсли;
	Для каждого ПользовательскоеПоле Из СтруктураЗадачи.customFields Цикл
		Если Не ПользовательскоеПоле.Свойство("value") ИЛИ Не ТипЗнч(ПользовательскоеПоле.value) = Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;	
		Если Не ПользовательскоеПоле.value.Свойство("name") Тогда
			Продолжить;
		КонецЕсли;
		Если ПользовательскоеПоле.value.name = ИмяСтадииДо Тогда
			ЗадачаВНужнойСтадии = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ЗадачаВНужнойСтадии;
КонецФункции

Процедура ПеренестиЗадачу(СтруктураПараметровТрекер, ПоляЗадачи)
	
	ИдентификаторЗадачи = ПоляЗадачи.id;

	СоответствиеПоля = Новый Соответствие();

	МассивИзменяемыхПолей = Новый Массив();
	СоответствиеИзменяегоПоля = Новый Соответствие();

	ПолучитьНовуюСтадию(СтруктураПараметровТрекер);

	СоответствиеНовогоЗначения = ПолучитьНовуюСтадию(СтруктураПараметровТрекер);

	Если СоответствиеНовогоЗначения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеИзменяегоПоля.Вставить("name", "State");
	СоответствиеИзменяегоПоля.Вставить("$type", "StateIssueCustomField");
	СоответствиеИзменяегоПоля.Вставить("value", СоответствиеНовогоЗначения);


	МассивИзменяемыхПолей.Добавить(СоответствиеИзменяегоПоля);

	СоответствиеПоля = Новый Соответствие();
	СоответствиеПоля.Вставить("customFields", МассивИзменяемыхПолей);

	json = Новый ПарсерJson;
	СтрокаТелаJSON = json.ЗаписатьJSON(СоответствиеПоля);


	СтруктураСоединения = ПолучитьСоединение(СтруктураПараметровТрекер);
	ТекстЗапросаЗадачи = "api/issues/" + ИдентификаторЗадачи;

	Соединение = СтруктураСоединения.Соединение;
	Заголовки = СтруктураСоединения.Заголовки;
	ЗапросХТТП = Новый HTTPЗапрос(ТекстЗапросаЗадачи);
	ЗапросХТТП.Заголовки = Заголовки;
	ЗапросХТТП.УстановитьТелоИзСтроки(СтрокаТелаJSON);

	Соединение.ОтправитьДляОбработки(ЗапросХТТП);



КонецПроцедуры

Функция ПолучитьНовуюСтадию(СтруктураПараметровТрекер)
	ИмяНовойСтадии = СтруктураПараметровТрекер.stageAfter;
	
	ТекстЗапросаЗадачи = "api/admin/customFieldSettings/bundles/state?fields=name,id,values(name,id,ordinal,isResolved),isUpdateable";
	СтруктураСоединения = ПолучитьСоединение(СтруктураПараметровТрекер);
	Соединение = СтруктураСоединения.Соединение;
	Заголовки = СтруктураСоединения.Заголовки;
	ЗапросХТТП = Новый HTTPЗапрос(ТекстЗапросаЗадачи);
	ЗапросХТТП.Заголовки = Заголовки;
	РезультатЗапроса = Соединение.Получить(ЗапросХТТП);
	ТелоЗапроса = РезультатЗапроса.ПолучитьТелоКакСтроку();
	
	ТелоЗапроса = СтрЗаменить(ТелоЗапроса, "$", "_");
	json = Новый ПарсерJson;
	ОбъектAPI = json.ПрочитатьJSON(ТелоЗапроса, , , Истина);
	
	Если ТипЗнч(ОбъектAPI) <> Тип("Массив") ИЛИ ОбъектAPI.Количество()=0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	МассивСостояний = ОбъектAPI[0].values;
	
	
	СоответствиеНовогоЗначения = Неопределено;
	
	Для каждого СостояниеЗадач Из МассивСостояний Цикл
		Если СостояниеЗадач.name = ИмяНовойСтадии Тогда
			СоответствиеНовогоЗначения = Новый Соответствие();
			СоответствиеНовогоЗначения.Вставить("isResolved", СостояниеЗадач.isResolved);
			СоответствиеНовогоЗначения.Вставить("name", СостояниеЗадач.name);
			СоответствиеНовогоЗначения.Вставить("id", СостояниеЗадач.id);
			СоответствиеНовогоЗначения.Вставить("$type", СостояниеЗадач._type);
			Прервать;
		КонецЕсли;	
	КонецЦикла;

	Возврат СоответствиеНовогоЗначения;
КонецФункции



Функция ПолучитьМассивЗадач(СтруктураПараметровТрекер, ТекстЗапросаЗадачи)

	СтруктураСоединения = ПолучитьСоединение(СтруктураПараметровТрекер);
	Соединение = СтруктураСоединения.Соединение;
	Заголовки = СтруктураСоединения.Заголовки;
	ЗапросХТТП = Новый HTTPЗапрос(ТекстЗапросаЗадачи);
	ЗапросХТТП.Заголовки = Заголовки;
	
		
	Попытка

		РезультатЗапроса = Соединение.Получить(ЗапросХТТП);
		ТелоЗапроса = РезультатЗапроса.ПолучитьТелоКакСтроку();
		ТелоЗапроса = СтрЗаменить(ТелоЗапроса, "$", "_");

		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоЗапроса); 

		МассивЗадач = ПрочитатьJSON(ЧтениеJSON, , , ); 

		ЧтениеJSON.Закрыть();
		
	Исключение
		МассивЗадач = Неопределено;
	КонецПопытки;

	Возврат МассивЗадач;
	
КонецФункции

Функция ПолучитьСоединение(СтруктураПараметровТрекер)

	Сервер = СтруктураПараметровТрекер.server;
	Токен  = "Bearer " + СтруктураПараметровТрекер.token;
	Соединение = Новый HTTPСоединение(Сервер, , , , , 100);

	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", Токен);
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Content-Type", "application/json");

	СтруктураСоединения = Новый Структура("Соединение, Заголовки", Соединение, Заголовки);

	Возврат СтруктураСоединения;
	
КонецФункции